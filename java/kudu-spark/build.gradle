// Licensed to the Apache Software Foundation (ASF) under one
// or more contributor license agreements.  See the NOTICE file
// distributed with this work for additional information
// regarding copyright ownership.  The ASF licenses this file
// to you under the Apache License, Version 2.0 (the
// "License"); you may not use this file except in compliance
// with the License.  You may obtain a copy of the License at
//
//   http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.

apply plugin: "scala"
apply from: "$rootDir/gradle/shadow.gradle"

dependencies {
  compile project(path: ":kudu-client", configuration: "shadow")
  compile libs.yetusAnnotations

  provided libs.scalaLibrary
  provided libs.sparkCore
  provided libs.sparkSql

  testCompile project(path: ":kudu-client", configuration: "shadowTest")
  testCompile libs.junit
  testCompile libs.scalatest
}

// Add compatibility sources based on the spark version used.
sourceSets {
  main {
    scala {
      if (versions.sparkBase == "1") {
        srcDir "src/main/spark1"
      } else if (versions.sparkBase == "2") {
        srcDir "src/main/spark2"
      } else {
        throw new GradleException("No compatibility sources exist for spark base version ${versions.sparkBase}")
      }
    }
  }
}

// Adjust the artifact name to match the maven build.
if (versions.sparkBase == "1") {
  archivesBaseName = "kudu-spark_${versions.scalaBase}"
} else {
  archivesBaseName = "kudu-spark${versions.sparkBase}_${versions.scalaBase}"
}